import("//build/config/features.gni")
import("//brave/components/brave_wayback_machine/buildflags/buildflags.gni")
import("//brave/components/brave_sync/buildflags/buildflags.gni")
import("//brave/components/ipfs/buildflags/buildflags.gni")

source_set("infobars") {
  # Remove when https://github.com/brave/brave-browser/issues/10649 is resolved
  check_includes = false
  sources = [
  ]

  deps = [
    "//base",
    "//brave/browser/ui",
    "//brave/app:brave_generated_resources_grit",
    "//chrome/app:chromium_strings",
    "//chrome/browser/ui",
    "//components/infobars/core",
    "//components/prefs",
    "//components/strings",
    "//ui/base",
    "//ui/views",
    "//url",
  ]

  if (ipfs_enabled) {
    sources += [
      "ipfs_infobar_delegate.cc",
      "ipfs_infobar_delegate.h",
    ]
    deps += [
      "//brave/components/ipfs",
      "//components/user_prefs"
    ]
  }

  if (enable_brave_wayback_machine) {
    sources += [
      "brave_wayback_machine_delegate_impl.cc",
      "brave_wayback_machine_delegate_impl.h",
    ]

    deps += [
      "//brave/components/brave_wayback_machine",
      "//brave/common:switches",
    ]
  }

  if (enable_brave_sync) {
    sources += [
      "sync_v2_migrate_infobar_delegate.cc",
      "sync_v2_migrate_infobar_delegate.h",
    ]

    deps += [
      "//brave/common:common",
      "//brave/components/brave_sync:features",
      "//brave/components/brave_sync:prefs",
    ]
  }
}
